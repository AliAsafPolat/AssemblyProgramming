PAGE 60,80
TITLE QUICK-SORT PROGRAM

STACKSG SEGMENT PARA STACK 'STACK'
 DW 20 DUP (?)
STACKSG ENDS

DATASG SEGMENT PARA 'DATA' 
	CR 		EQU 13
	LF 		EQU 10
	ISMIM	DB 'ALI ASAF POLAT 16011079   ','$'
	MSG0	DB 'Dizi boyutunu belirleyiniz.',0
	MSG1	DB 'Dizi elemanini giriniz.',0

	HATA 	DB 'Girdiginiz eleman -128,127 arasinda degil veya tamsayi degil!Yeni giris:',0
	EKSI	DB '-','$'
	SUNUM1  DB '  Girdiginiz degerler: ','$'
	SUNUM2  DB '  Siralanmis hali: ','$'
	DIZI	DB 100 DUP(?)
	BOYUT 	DB ?
	
	
DATASG ENDS


CODESG SEGMENT PARA 'CODE'
	ASSUME CS:CODESG,DS:DATASG,SS:STACKSG
	
BASLA PROC FAR
	
			PUSH DS			;DONUS ICIN GEREKLI
			XOR AX,AX		;OLAN DEGERLER
			PUSH AX			;YIGINDA SAKLANIYOR.
			
			MOV AX,DATASG	;DATASG TANIMLI OLAN
			MOV DS,AX		;KISMA ERISMEK ICIN.
			
			LEA DX,ISMIM	;EKRANA ISMIMI BASMAK ICIN KESME.
			MOV AH,09H
			INT 21H			
			
			XOR AX,AX
			XOR DX,DX
			XOR CX,CX
			
			MOV AX,OFFSET MSG0	;DIZININ BOYUTUNU ISTEYEN MESAJ  
			CALL PUT_STR		;YAZDIRILDI.	
			CALL GETN
			
			MOV CL,AL			;DIZININ BOYUTU CL'YE ATANDI. 
			MOV BOYUT,AL		;AYRICA BOYUT DEGISKENINE YERLESTIRILDI.
			XOR AX,AX
			XOR SI,SI			;DİZİNİN BASLANGIC DEGERINE GITTIM.
	DİZİ_LOOP:	
					
			MOV AX,OFFSET MSG1
			CALL PUT_STR 	;MESAJ1'I GOSTER. 	
			XOR AX,AX		;AX SIFIRLANDI.
			CALL GETN		;DİZİ ELEMANINI OKU.
			MOV DIZI[SI],AL;ALINAN DEGER DIZININ ILGILI OFFSETINE YAZILDI.
			INC SI
			XOR AX,AX		;SAGLAMA ICIN.
			
			LOOP DİZİ_LOOP	;ARTIK TUM ELEMANLARIM DIZIDE.
			
			
			
			LEA DX,SUNUM1	;DOSBOX TERMINALDE 
			MOV AH,09H		;GIRILEN DEGERLER 
			INT 21H			;YAZDIRMAK ICIN KESME
			
			
			
			
			
			XOR SI,SI		;KULLANACAGIM REGISTERLARI SIFIRRLIYORUM.
			XOR CX,CX
			XOR AX,AX
			
			XOR SI,SI		;DİZİYİ EKRANA YAZDIRMAK ICIN.
			MOV CL,BOYUT	;DIZININ BOYUTUNU ATIYORUM.
			
			
	YAZDIR:				
			MOV AL,DIZI[SI]	;YAZDIRMAK ISTEDIGIM ELEMANI ATIYORUM.	
			CMP AL,0		;EGER SAYI NEGATIF ISE EKRANA '-' BASTIRACAGIM
			JGE EKSIDEGER1	;NEGATIF DEGILSE ZIPLATIYORUM 
			LEA DX,EKSI	;EKRANA EKSI BASMAK ICIN KESME.
			MOV AH,09H
			INT 21H	
			
			MOV AH,255	;NEGATIF DEGERI 255 DEN ÇIKARTIP 1 EKLEDIGIMDE 
			SUB AH,AL	;POZITIF HALINI ALMIS OLUYORUM.EKRANA ZATEN '-' BASILMIŞTI.
			MOV AL,AH	;ÇIKARMA SONUCUNU AL'YE ATIP AH'I SIFIRLIYORUM.
			XOR AH,AH	
			INC AL		;+1 EKLEME GERCEKLENDI.
EKSIDEGER1:		
			CALL PUTN		;YAZDIRACAK YORDAM CAGIRILDI.
			XOR AX,AX		;ONCEKI DEGERIMI TEMIZLIYORUM.
					
			INC SI			;BIR SONRAKI DIZI ELEMANINA GIDIYORUM.
			LOOP YAZDIR
			
			MOV BL,0		;LOW DEGERIMI
			MOV BH,BOYUT	;HİGH DEGERİM
			DEC BH			;QUICKSORTA GIDERKEN HIGH-1 DEGERIMI ATIYORUM.
			CALL QUICKSORT	;REGISTERLAR UZERINDEN DEGER GONDERMEK ISTIYORUM.
			
			
			LEA DX,SUNUM2		;DOSBOX TERMINALDE 'SIRALANMIS HALI'
			MOV AH,09H			;YAZDIRMAK ICIN KULLANDIGIM KESME
			INT 21H
			
			
			
			
			XOR SI,SI			;KULLANACAGIM REGISTERLARI SIFIRLIYORUM	
			XOR CX,CX
			XOR AX,AX
			
		;**QUICKSORT UYGULANDIKTAN SONRA TEKRAR EKRANA YAZDIRIYORUM**
		
			MOV CL,BOYUT		;DIZININ BOYUTUNU ATIYORUM.
			
	YAZDIR1:			
			MOV AL,DIZI[SI]		;YAZDIRMAK ISTEDIGIM ELEMANI ATIYORUM.	
			CMP AL,0
			JGE EKSIDEGER		;EGER NEGATIF DEGILSE ZIPLATIYORUM
			LEA DX,EKSI			;EKRANA EKSI BASMAK ICIN KESME.
			MOV AH,09H
			INT 21H	
			
			MOV AH,255			;SAYI NEGATIFSE 255 DEN ÇIKARIP 1 EKLEDIGIMDE	
			SUB AH,AL			;POZITIF HALINI ELDE ETMIS OLUYORUM.
			MOV AL,AH			;ZATEN '-' YAZDIRILMIŞTI.
			XOR AH,AH			;AH DEGERIMI SIFIRLIYORUM.KARISIKLIK ONLENIYOR.
			INC AL				;+1 EKLEME GERCEKLENDI.
EKSIDEGER:					
			
			CALL PUTN		;YAZDIRACAK YORDAM CAGIRILDI.
			XOR AX,AX		;ONCEKI DEGERIMI TEMIZLIYORUM.
					
			INC SI			;BIR SONRAKI DIZI ELEMANINA GIDIYORUM.
			LOOP YAZDIR1
			
			;******************************DIZI ALMA KISMI BİTTİ SORT KISMI BASLIYOR******************************
			

			
			
			
	RETF
BASLA ENDP

QUICKSORT PROC NEAR
		
			CMP BL,BH		;LOW VE HIGH DEGERLERINI KARSILASTIRIYORUM
			JGE SON			;EGER LOW BUYUK VEYA HIGH DEGERINE ESITSE SON'A.
			PUSH BX			;BH VE BL DEGERLERIMI KAYBETMEMEK ICIN YIGINA ATIYORUM.
			
			CALL PARTITION  
			XOR CX,CX
			MOV CL,AL		;PARTITIONDAN DONEN AL DEGERINI CL YE ATTIM.
			
			PUSH CX			;CL DEGERINI DIGER QUICKSORTA VEREBILMEK ICIN YIGINDA SAKLIYORUM.
			
			DEC CL			;ILK QUICKSORTA PI-1 DEGERI GIDECEKTI.	
			
			MOV BH,CL		;HIGH DEGERIME PI-1 DEGERINI ATTIM.
			
			CALL QUICKSORT	;QUICKSORT YORDAMINI CAGIRDIM.
			
			POP CX			;2.QUIKSORTTAN ONCE CL DEGERIMI ALDIM.
			POP BX			;LOW VE HIGH DEGERLERIM GERI ALINDI
			
			INC CL			;IKINCI QUICKSORTA PI+1 DEGERI GIDECEKTI.
			MOV BL,CL		;LOW DEGERIM PI+1 DEGERINI ALDI.
			CALL QUICKSORT	;QUICKSORT YORDAMI TEKRAR ÇAĞIRILDI
			
			
SON:	
			RET				;RET ILE BITIRIYORUM.

QUICKSORT ENDP	

PARTITION PROC NEAR
			PUSH BX
			PUSH CX
			PUSH DX
			
			;BL LOW
			;BH HIGH	
			;DH PIVOT
			
			;DI ->J
			;SI ->İ
			XOR AX,AX 		;DI YA DOGRUDAN BH ATAMIYORUM				
			MOV AL,BH		;AL UZERINDEN ATACAGIM
			MOV DI,AX		;HIGH DEGERIM DI YA ATANDI
			MOV DH,DIZI[DI]	;PIVOT DEGERIM ARTIK DH.(PIVOT=ARR[HIGH]) 
			
			XOR AX,AX		;BL DEGERINI SI'YA DOGRUDAN ATAMADIGIM
			MOV AL,BL		;ICIN AX REGISTERI UZERINDEN ATACAGIM.
			DEC AL			;BASLANGIC INDISI LOW-1=İ OLACAK
			CMP AL,0FFH		;BURADAKI AL DEGERIM -1 OLUYORSA 
			JNE FF_DEGIL	;SI DEGERINI DE FFFF YAPMAK ISTIYORUM.
			MOV AH,AL
			
FF_DEGIL:	MOV SI,AX		;SI DEGERIM INDEX DEGERI YANİ =i
			
			XOR CX,CX		;DONGU DEGISKENI ATANACAK.
			MOV CL,BH 		;DONGU DEGERIM HIGH ATANDI.AMA HIGH-LOW KERE DONMELI. 
			SUB CL,BL		;DONGU DEGERIM HIGH-LOW GERCEKLENDI.
			
			
			XOR DI,DI		;DI'YI KULLANACAGIM.SIFIRLADIM.	
			XOR AX,AX		;DONGUNUN İLK DEGERI OLAN LOW DEGERINI
			MOV AL,BL		;DI YA ATMALIYIM BUNUN ICIN AX UZERINDEN
			MOV DI,AX		;DI REGISTERINA GECIRDIM.	
			
PART_LOOP:	
			CMP DIZI[DI],DH	;PIVOT DEGERIMLE DIZININ SIRADAKI ELEMANI CMP.		
			JG PART_SON		;BUYUK ISE ISLEM YOK.	
			INC SI			;DEGIL ISE SI DEGERIM SI+1.
			XOR AX,AX
			MOV AL,DIZI[SI] ;SWAP ISLEMI YAPILACAK.
			MOV AH,DIZI[DI] ;DEGERLER ARTIK AX REGISTERDA
			MOV DIZI[SI],AH
			MOV DIZI[DI],AL ;SWAP GERCEKLESTI.
PART_SON:			
			INC DI			;DONGU INDISIMI ARTIRIYORUM(BYTE)
			LOOP PART_LOOP
			            
			INC SI			;LOOPTAN SONRAKI SWAP ISLEMI ICIN SI+1
			XOR DI,DI
			XOR AX,AX 		;DI YA DOGRUDAN BH ATAMIYORUM				
			MOV AL,BH		;AL UZERINDEN ATACAGIM
			MOV DI,AX		;DI=BH BUNU ARR[HIGH]'A ERISMEK ICIN KULLANACAGIM
			
			XOR AX,AX
			MOV AL,DIZI[SI]	;SWAP ISLEMINI YAPACAGIM.
			MOV AH,DIZI[DI]	;DEGERLERIM ARTIK AX UZERINDE.
			MOV DIZI[SI],AH	
			MOV DIZI[DI],AL	;SWAP ISLEMIM GERCEKLESTI.
			
			MOV AX,SI		;RETURN DEGERI ICIN AX DONDURUYORUM.
			
			
			POP DX			;YUKARIDA PUSHLADIGIM SEKILDE  TERS 
			POP CX			;SIRAYLA TEKRAR POP ISLEMINI GERCEKLESTIRIYORUM.
			POP BX
			
			RET				;YORDAMDAN CIKIYORUM.
PARTITION ENDP


;***DIZIYE ELEMAN ALMA YAZDIRMA KISMI BASLIYOR.***

GETC PROC NEAR				
			MOV AH,1H
			INT 21H
			RET
GETC ENDP

PUTC PROC NEAR
			PUSH AX				;AX DX YAZMACLARININ DEGERLERINI
			PUSH DX				;KORUMAK ICIN PUSH ISLEMI YAPILIR.
			MOV DL,AL	
			MOV AH,2	
			INT 21H				;GEREKLI VERIYI YAZDIRMAK ICIN YAPILAN ISLEMLER.
			POP DX		
			POP AX
			RET
PUTC ENDP 

PUT_STR PROC NEAR				;AX DE ADRESI VERILEN SONUNDA 0 OLAN
								;DIZGEYI KARAKTER KARAKTER YAZDIRIR.	
			PUSH BX				;BX DIZGE INDISI OLARAK KULLANILACAK
			MOV BX,AX			;ADRESI BX E AL.
			MOV AL,BYTE PTR[BX]	;AL'DE ILK KARAKTER VAR.
PUT_LOOP:	CMP AL,0		
			JE PUT_FIN			;0 GELDI ISE DIZGE SONA ERDI DEMEK.
			CALL PUTC			;AL'DEKI DEGERI EKRANA YAZAR.
			INC BX				;BİR SONRAKI KARAKTERE GEC.
			MOV AL,BYTE PTR[BX]
			JMP PUT_LOOP		;YAZDIRMAYA DEVAM ET.
PUT_FIN:
			POP BX				;BX'I GERI CEKIYORUM.
			RET
PUT_STR ENDP

GETN PROC NEAR					;KLAVYEDEN BASILAN DEGERI OKUR.SONUC AX'DE DONER.
; DX:SAYININ ISARETLI OLUĞ OLMADIGINI BELIRLER.1(+) -1(-).
; BL:HANE BILGISINI TUTAR.
; CX:OKUNAN SAYININ ISLENMESI SIRASINDA ARADEGERI TUTAR.				
; AL:KLAVYEDEN OKUNAN KARAKTERI TUTAR
; AX DONUS DEGERI OLARAK DEGISMEK DURUMUNDA FAKAT DIGER DEGERLER 
; KORUNMALIDIR.	

			PUSH BX
			PUSH CX
			PUSH DX
GETN_START: 
			MOV DX,1 			;SAYININ POZİTİF OLDUGUNU DUSUNELIM.
			XOR BX,BX			;OKUMA YAPMADI HANE 0 OLUR.
			XOR CX,CX			;ARA TOPLAM DEGERI 0 DIR.
NEW:		
			CALL GETC			;KLAVYEDEN ILK DEGERI AL'YE OKU.			
			CMP AL,CR
			JE FIN_READ			;ENTER TUSUNA BASILMIS ISE OKUMA BITER.
			CMP AL,'-'			;AL '-' GELDIYSE.
			JNE CTRL_NUM		;GELEN SAYI 0-9 ARASI MI?
NEGATIVE:	
			MOV DX,-1			;- BASILDIYSA DX=-1	
			JMP NEW
CTRL_NUM:	
			CMP AL,'0'			;SAYININ 0-9 ARASINDA OLDUGUNU KONTROL ET.	
			JB HATALI
			CMP AL,'9'
			JA HATALI			;DEGIL ISE HATA MESAJI.
			SUB AL,'0'			;RAKAM ALINDI HANEYI TOPLAMA DAHIL EDIYOR.
			MOV BL,AL			;BL YE OKUNAN DEGERI KOY.
			MOV AX,10			;HANEYI EKLERKEN *10 YAPAR.
			PUSH DX				;MUL KOMUTUNDAN ETKILENMEMESI ICIN.
			MUL CX
			POP DX
			MOV CX,AX			;CX'DEKI ARADEGER 10 ILE CARPILDI.
			ADD CX,BX			;OKUNAN HANE ARA DEGERE EKLENDI.
			JMP NEW				;KLAVYEDEN YENI BASILAN DEGER ALINDI.
HATALI:		
			MOV AX,OFFSET HATA
			CALL PUT_STR 		;HATA MESAJINI GOSTERIR.
			JMP GETN_START		;O ANA KADAR YAPILANLARI UNUT YENIDEN SAYI ALMAYA BASLA.
			
FIN_READ:			
			MOV AX,CX			;SONUC AX UZERINDEN DONECEK
			CMP DX,1			;ISARETE GORE SAYI AYARLANACAK
			JE FIN_GETN
			NEG AX				;AX=-AX.
			
FIN_GETN:	
			CMP AX,127			;EGER SAYI 127 DEN BUYUKSE HATA MESAJI ALACAK.
			JG  HATALI
			
			CMP AX,-128			;EGER SAYI -128DEN KUCUKSE HATA MESAJI ALACAK.	
			JL  HATALI

			POP DX
			POP CX
			POP BX 
			
			RET
GETN ENDP

PUTN PROC NEAR 					;AX DE BULUNAN SAYIYI EKRANA YAZDIRIR.
			
			PUSH CX
			PUSH DX				;DX BOLUMDEN ETKILENMESIN DIYE.
			XOR DX,DX
			PUSH DX				;KAC HANE ALACAGIMIZI BILMEDIGIMIZDEN 
								;YIGINA 0 KOYUP ONU ALANA KADAR DEVAM EDELIM.				
			
			MOV CX,10
			CMP AX,0
			JGE CALC_DIGITS
			NEG AX  			;SAYI NEG İSE POZITIF YAPILIR
			PUSH AX				;AX I SAKLA
			MOV AL,'-'			;ISARETI EKRANA YAZDIR.
			CALL PUTC			
			POP AX 				;AX I GERI AL
CALC_DIGITS:	
			DIV CX				;BOLUM=AX KALAN=DX
			ADD DX,'0'			;KALAN DEGERI ASCII OLARAK BUL.
			PUSH DX
			XOR DX,DX
			CMP AX,0			;BOLEN 0 ISE ISLEM BITTI DEMEKTIR.
			JNE CALC_DIGITS		;ISLEMI TEKRARLA.
			
DISP_LOOP:	
			;YAZILACAK TUM HANELER YIĞINDA.SIRA SIRA ALALIM.
			POP AX
			CMP AX,0			;AX=0 OLDUGUNDA SONA ERDI DEMEKTIR.
			JE END_DISP_LOOP
			CALL PUTC 			;AL'DEKI DEGERI YAZ.
			JMP DISP_LOOP		;ISLEMI TEKRARLA
END_DISP_LOOP:
			
			POP DX				;YIGINDAN DEGERLERIMI ALIYORUM.
			POP CX
			RET
PUTN ENDP			
			
CODESG ENDS
END BASLA
			
	
	
	
	